begin
procedure swap(a: int[], i: int, j: int) =
     { tmp: int; tmp := a[i]; a[i] := a[j]; a[j] := tmp},

procedure printA(a: int[], len:int) = {i: int;  i:= 0; do ! i=len -> print a[i]; i:= i+1 od},

function partition(a : int[], x0 : int, i: int, j: int) : int =
{ do i<=j && a[i] <= x0 -> i:= i+1
   | i<=j               -> swap(a, i, j); j:= j-1
  od ;
  return i },

procedure qs(a: int[], i: int, j:int) =
{ p: int ; 
  if i<j -> p:= partition(a, a[i], i+1, j);
            swap(a,i,p-1);
		    qs(a,i,p-2);
		    qs(a,p,j)
  | true -> skip
  fi },

procedure quicksort(a: int[], len: int) = qs(a, 0, len - 1) ,

a: int[15],
len: int, 
i: int;
i:= 0;
len:= 15;

do i<len -> a[i] := len-i; i:= i+1 od;
printA(a, len);
a[1]:= 3;

quicksort(a,len);
printA(a, len)
end

   

